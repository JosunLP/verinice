--- RCPTT testcase ---
Format-Version: 1.0
Contexts: _kD9CUIxPEei2UKUDmonytQ,_74j9YLaIEeiiZL0aNB5Nyg,_lGnj8Ix1Eei2UKUDmonytQ,_4GRncI0bEei2UKUDmonytQ
Element-Name: test-generate-report-dialog
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _rI7VYASsEeupd5XMAdmhgA
Runtime-Version: 2.5.5.202409072058
Save-Time: 1/7/25, 1:13 PM
Tags: bp
Testcase-Type: ecl
Verifications: _wm5P0CtBEeuMltfUUtdvug

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

bp-import-vna "Informationsverbund-1.vna"
wait-for-import "Modernisierter IT-Grundschutz" "importierte Objekte/Informationsverbund-1 \\[b71753\\]"

get-menu -path "Datei/Report erzeugen..." | click

proc "select-report-file" [val item ]{
    get-window Report | get-group "" | get-object | invoke getChildren | invoke  get 1| invoke setSelectedIndexByName $item
}

with [get-window Report] {
    let [val scopeCompo [get-group "" |get-combo -after [get-label "Geltungsbereich:"]]]
            [val reportText [get-group "" | get-editbox -after [get-label "Report auswählen:"]]]
            [val ouputFormatCompo [get-group "" | get-combo -after [get-label "Ausgabeformat"]]]
            [val resetButton [get-group "" | get-button "Zurücksetzen"]]{
        // combo boxes should be empty when opening the dialog
        $scopeCompo | get-property selection | is-empty | verify-true
        $reportText | get-property text | is-empty | verify-true
        $ouputFormatCompo | get-property selection | is-empty | verify-true


        // when selecting a matching report, the output format should be auto-filled
        $scopeCompo | select "Informationsverbund-1"
        select-report-file "(S) ITGS: A.1 Strukturanalyse"
        $ouputFormatCompo | get-property selection | equals "Portable Document Format (PDF)" | verify-true

        // all combo boxes should be reset when clicking the button
        $resetButton | click
        $scopeCompo | get-property selection | is-empty | verify-true
        $reportText | get-property text | is-empty | verify-true
        $ouputFormatCompo | get-property selection | is-empty | verify-true

        // when selecting only a report, the output format should be auto-filled
        select-report-file "(S) ITGS: A.1 Strukturanalyse"
        $ouputFormatCompo | get-property selection | equals "Portable Document Format (PDF)" | verify-true
        $scopeCompo | get-property selection | is-empty | verify-true

        // when switching to a report that does not support the format, a matching one should be selected
        select-report-file "(S) ISM-ISO: Export: Aufgaben (de)"
        $ouputFormatCompo | get-property selection | equals "Excel Format (XLSX)" | verify-true
        $scopeCompo | get-property selection | is-empty | verify-true

        // when switching back to a report that supports all formats, the first available format is selected
        select-report-file "(S) ITGS: A.1 Strukturanalyse"
        $ouputFormatCompo | get-property selection | equals "Portable Document Format (PDF)" | verify-true
        $scopeCompo | get-property selection | is-empty | verify-true
        $resetButton | click

        // changing the report does not change the scope
        $scopeCompo | select "Informationsverbund-1"
        select-report-file "(S) ITGS: A.1 Strukturanalyse"
        $scopeCompo | get-property selection | equals "Informationsverbund-1" | verify-true
        select-report-file "(S) ITGS: A.2 Schutzbedarfsfeststellung"
        $scopeCompo | get-property selection | equals "Informationsverbund-1" | verify-true
        
        // when changing the report in the via rcptt method and the scope did not match the selected scope the scope is cleared 
        select-report-file "(S) ITGS-alt: A.1 Strukturanalyse"
        $scopeCompo | get-property selection | is-empty | verify-true
        
        // when switching to a reports with no common format no is selected
        select-report-file "(S) ISM-ISO: Export: Aufgaben (de),(S) ISM-ISA: Information Security Assessment 6 Report detailliert"
        $ouputFormatCompo | get-property selection | is-empty | verify-true
        $scopeCompo | get-property selection | is-empty | verify-true
        
    }
    get-button "Abbrechen" | click

}

delete-bpelement "importierte Objekte/Informationsverbund-1 \\[b71753\\]"

verify-error {get-window Fehler }

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
